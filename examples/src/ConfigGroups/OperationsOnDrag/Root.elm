module ConfigGroups.OperationsOnDrag.Root exposing
    ( Model
    , Msg
    , demoView
    , init
    , initialModel
    , subscriptions
    , update
    , url
    , view
    )

import ConfigGroups.OperationsOnDrag.InsertAfter
import ConfigGroups.OperationsOnDrag.InsertAround
import ConfigGroups.OperationsOnDrag.InsertBefore
import ConfigGroups.OperationsOnDrag.Rotate
import ConfigGroups.OperationsOnDrag.Swap
import Html
import Html.Attributes
import Html.Events



-- MODEL


type alias Model =
    { id : Int
    , examples : List Example
    }


type Example
    = InsertAfter ConfigGroups.OperationsOnDrag.InsertAfter.Model
    | InsertBefore ConfigGroups.OperationsOnDrag.InsertBefore.Model
    | InsertAround ConfigGroups.OperationsOnDrag.InsertAround.Model
    | Rotate ConfigGroups.OperationsOnDrag.Rotate.Model
    | Swap ConfigGroups.OperationsOnDrag.Swap.Model


initialModel : Model
initialModel =
    { id = 0
    , examples =
        [ InsertAfter ConfigGroups.OperationsOnDrag.InsertAfter.initialModel
        , InsertBefore ConfigGroups.OperationsOnDrag.InsertBefore.initialModel
        , InsertAround ConfigGroups.OperationsOnDrag.InsertAround.initialModel
        , Rotate ConfigGroups.OperationsOnDrag.Rotate.initialModel
        , Swap ConfigGroups.OperationsOnDrag.Swap.initialModel
        ]
    }


init : () -> ( Model, Cmd Msg )
init _ =
    ( initialModel, Cmd.none )


url : Int -> String
url id =
    case id of
        0 ->
            "https://raw.githubusercontent.com/annaghi/dnd-list/master/examples/src/ConfigGroups/OperationsOnDrag/InsertAfter.elm"

        1 ->
            "https://raw.githubusercontent.com/annaghi/dnd-list/master/examples/src/ConfigGroups/OperationsOnDrag/InsertBefore.elm"

        2 ->
            "https://raw.githubusercontent.com/annaghi/dnd-list/master/examples/src/ConfigGroups/OperationsOnDrag/InsertAround.elm"

        3 ->
            "https://raw.githubusercontent.com/annaghi/dnd-list/master/examples/src/ConfigGroups/OperationsOnDrag/Rotate.elm"

        4 ->
            "https://raw.githubusercontent.com/annaghi/dnd-list/master/examples/src/ConfigGroups/OperationsOnDrag/Swap.elm"

        _ ->
            ""



-- UPDATE


type Msg
    = LinkClicked Int
    | InsertAfterMsg ConfigGroups.OperationsOnDrag.InsertAfter.Msg
    | InsertBeforeMsg ConfigGroups.OperationsOnDrag.InsertBefore.Msg
    | InsertAroundMsg ConfigGroups.OperationsOnDrag.InsertAround.Msg
    | RotateMsg ConfigGroups.OperationsOnDrag.Rotate.Msg
    | SwapMsg ConfigGroups.OperationsOnDrag.Swap.Msg


update : Msg -> Model -> ( Model, Cmd Msg )
update message model =
    case message of
        LinkClicked id ->
            ( { model | id = id }, Cmd.none )

        _ ->
            model.examples
                |> List.map
                    (\example ->
                        case ( message, example ) of
                            ( InsertAfterMsg msg, InsertAfter mo ) ->
                                stepInsertAfter (ConfigGroups.OperationsOnDrag.InsertAfter.update msg mo)

                            ( InsertBeforeMsg msg, InsertBefore mo ) ->
                                stepInsertBefore (ConfigGroups.OperationsOnDrag.InsertBefore.update msg mo)

                            ( InsertAroundMsg msg, InsertAround mo ) ->
                                stepInsertAround (ConfigGroups.OperationsOnDrag.InsertAround.update msg mo)

                            ( RotateMsg msg, Rotate mo ) ->
                                stepRotate (ConfigGroups.OperationsOnDrag.Rotate.update msg mo)

                            ( SwapMsg msg, Swap mo ) ->
                                stepSwap (ConfigGroups.OperationsOnDrag.Swap.update msg mo)

                            _ ->
                                ( example, Cmd.none )
                    )
                |> List.unzip
                |> (\( examples, cmds ) -> ( { model | examples = examples }, Cmd.batch cmds ))


stepInsertAfter : ( ConfigGroups.OperationsOnDrag.InsertAfter.Model, Cmd ConfigGroups.OperationsOnDrag.InsertAfter.Msg ) -> ( Example, Cmd Msg )
stepInsertAfter ( mo, cmds ) =
    ( InsertAfter mo, Cmd.map InsertAfterMsg cmds )


stepInsertBefore : ( ConfigGroups.OperationsOnDrag.InsertBefore.Model, Cmd ConfigGroups.OperationsOnDrag.InsertBefore.Msg ) -> ( Example, Cmd Msg )
stepInsertBefore ( mo, cmds ) =
    ( InsertBefore mo, Cmd.map InsertBeforeMsg cmds )


stepInsertAround : ( ConfigGroups.OperationsOnDrag.InsertAround.Model, Cmd ConfigGroups.OperationsOnDrag.InsertAround.Msg ) -> ( Example, Cmd Msg )
stepInsertAround ( mo, cmds ) =
    ( InsertAround mo, Cmd.map InsertAroundMsg cmds )


stepRotate : ( ConfigGroups.OperationsOnDrag.Rotate.Model, Cmd ConfigGroups.OperationsOnDrag.Rotate.Msg ) -> ( Example, Cmd Msg )
stepRotate ( mo, cmds ) =
    ( Rotate mo, Cmd.map RotateMsg cmds )


stepSwap : ( ConfigGroups.OperationsOnDrag.Swap.Model, Cmd ConfigGroups.OperationsOnDrag.Swap.Msg ) -> ( Example, Cmd Msg )
stepSwap ( mo, cmds ) =
    ( Swap mo, Cmd.map SwapMsg cmds )



-- SUBSCRIPTIONS


subscriptions : Model -> Sub Msg
subscriptions model =
    model.examples
        |> List.map
            (\example ->
                case example of
                    InsertAfter mo ->
                        Sub.map InsertAfterMsg (ConfigGroups.OperationsOnDrag.InsertAfter.subscriptions mo)

                    InsertBefore mo ->
                        Sub.map InsertBeforeMsg (ConfigGroups.OperationsOnDrag.InsertBefore.subscriptions mo)

                    InsertAround mo ->
                        Sub.map InsertAroundMsg (ConfigGroups.OperationsOnDrag.InsertAround.subscriptions mo)

                    Rotate mo ->
                        Sub.map RotateMsg (ConfigGroups.OperationsOnDrag.Rotate.subscriptions mo)

                    Swap mo ->
                        Sub.map SwapMsg (ConfigGroups.OperationsOnDrag.Swap.subscriptions mo)
            )
        |> Sub.batch



-- VIEW


view : Model -> Html.Html Msg
view model =
    model.examples
        |> List.indexedMap (demoWrapperView model.id)
        |> Html.section []


demoWrapperView : Int -> Int -> Example -> Html.Html Msg
demoWrapperView currentId id example =
    Html.div
        [ Html.Attributes.style "display" "flex"
        , Html.Attributes.style "flex-wrap" "wrap"
        , Html.Attributes.style "justify-content" "center"
        , Html.Attributes.style "margin" "4em 0"
        ]
        [ demoView example
        , Html.div
            [ Html.Attributes.classList [ ( "link", True ), ( "is-active", id == currentId ) ]
            , Html.Events.onClick (LinkClicked id)
            ]
            [ Html.text (info example) ]
        ]


demoView : Example -> Html.Html Msg
demoView example =
    case example of
        InsertAfter mo ->
            Html.map InsertAfterMsg (ConfigGroups.OperationsOnDrag.InsertAfter.view mo)

        InsertBefore mo ->
            Html.map InsertBeforeMsg (ConfigGroups.OperationsOnDrag.InsertBefore.view mo)

        InsertAround mo ->
            Html.map InsertAroundMsg (ConfigGroups.OperationsOnDrag.InsertAround.view mo)

        Rotate mo ->
            Html.map RotateMsg (ConfigGroups.OperationsOnDrag.Rotate.view mo)

        Swap mo ->
            Html.map SwapMsg (ConfigGroups.OperationsOnDrag.Swap.view mo)



-- EXAMPLE INFO


type alias Info =
    String


info : Example -> Info
info example =
    case example of
        InsertAfter _ ->
            "Insert after"

        InsertBefore _ ->
            "Insert before"

        InsertAround _ ->
            "Insert Around"

        Rotate _ ->
            "Rotate"

        Swap _ ->
            "Swap"
